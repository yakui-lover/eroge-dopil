init 2:
    $ d1_keys = False
    $ d1_dv_punishment = False
    $ d1_mt_good_relationship = False
    $ set_name('ug', u"Пионер")

label day1:

    $ backdrop = 'days'

    $ new_chapter(1, u"День первый")
    $ day_time()

    $ make_names_unknown()

    scene bg black 

    play music music_list["no_tresspassing"] fadein 0

    $ renpy.pause(2)

    $ persistent.sprite_time = 'day'
    scene bg int_bus 
    with flash

    window show
    "Я проснулся, почувствовав согревающее прикосновение солнца."
    "Что-то было не так."
    th "Ну я и соня. Меня даже давешний скрип немазаных тормозов не..."
    th "...Разбудил. Стоп. Остановку, что ли, проехал?"
    me "Чёрт, моя остановка!"
    "Я машинально подскочил и ринулся к двери."
    "Но двери не было, как и водителя."
    "Я оглядел автобус и понял, что это не привычный мне потрепанный «ЛИаЗ», а самый настоящий «Икарус»!{w} На вид новый, но пропахший бензином. Я как будто вернулся в девяностые."
    "Мягкие ворсистые панели, высокие спинки, занавески и неуловимый дух «братских стран» во всём — то явно был рейсовый «Икарус», который останавливается только когда мальчикам нужно налево, девочкам — направо."
    "На несколько мгновений я утратил связь с реальностью."
    th "Как?..{w} Что?..{w} Но я же?.."
    th "Меня похитили?"
    th "Может я... умер?"

    play sound sfx_punch_medium

    with vpunch
    "Я недоверчиво хлопнул себя по щеке. Наваждение не прошло, и я с силой приложился о подголовник ближайшего кресла." 
    "Значит, способность воспринимать внешние раздражители меня не покинула.{w} Зрение, обоняние, осязание на месте..."
    "Нет, такая боль не может быть плодом воображения."
    th "Итак, всё же похищение?"
    th "Крайне сомнительно. Не уверен, что я нужен даже своим родным."
    th "Но факт есть факт - меня вывезли чёрт знает куда, пока я спал."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_bus 
    with dissolve

    window show
    "Выбравшись наружу, я огляделся."
    "Мое окружение не оставляло мне большого простора для мыслей."
    th "Лето!{w} Век сгущёнки не видать — настоящее лето!{w} Невозможно..."
    "Я приложил руку к разгорячённому лбу."
    "Голову пронзила боль — то ли от непривычных мне умственных усилий, то ли от того, что я треснулся в автобусе."
    "Постепенно в ней проявлялись воспоминания."
    window hide

    scene anim intro_14 
    with fade2

    play sound sfx_intro_bus_transition

    $ renpy.pause(3, hard=True)
    scene anim intro_15 
    with fade
    $ renpy.pause(3, hard=True)

    play ambience ambience_day_countryside_ambience fadein 5

    scene anim intro_16 
    with fade
    $ renpy.pause(3)

    window show
    "Длинная дорога, кажущаяся бесконечной, леса и поля, сливающиеся в единое жёлто-зелёное месиво."
    "Девушка...{w} Она говорила мне что-то!"

    stop ambience fadeout 3

    "Боль в висках была нестерпимой."
    window hide

    $ renpy.pause(2)

    $ persistent.sprite_time = 'day'
    scene bg ext_bus 
    with fade2

    window show
    th "Эта девушка..."
    th "Галлюцинации? Она что, привиделась мне? Я брежу?"
    "Но стоило подумать о ней, как стало гораздо спокойнее, головная боль слегка ослабла."
    th "Если девушка реальна, то, она могла быть причастна к моему похищению."
    th "Не вижу никого вокруг."
    th "Это шанс. Нужно выбираться отсюда!"
    "Быстро прикинув в уме, я торопливо бросился в сторону, откуда, судя по всему, приехал автобус."

    stop music fadeout 3

    "..."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_road_day 
    with dissolve2

    play ambience ambience_ext_road_day fadein 3

    window show
    "..."
    "В конце концов, физические упражнения никогда не были моей сильной стороной."
    "Не пробежав и ста метров, я свернул к обочине и устало рухнул на траву отдышаться."
    "Моя неоконченная стометровка позволила ещё раз обдумать произошедшее и унять панические мысли."
    th "Я и правда собираюсь выбраться отсюда на своих двоих?"
    "Поверх успокаивающе-зелёных красок открывающегося мне пейзажа я не мог разглядеть ни одного здания."
    me "Никого в радиусе нескольких километров..."
    "Я узнал дорогу, змеёй вытянувшуюся к горизонту.{w} По ней меня сюда привезли."
    th "Совершить такое на одном автобусе нереально."
    "Это, очевидно, другая страна, хотя «Икарус» сильно путал мои умозаключения."
    "Но здесь точно не мой чёртов климатический пояс!"
    "Меня везли самолетом. Это единственное объяснение."
    "Сняли с моего автобуса, посадили в самолет, затем опять в автобус. И где я сейчас?"
    "Я никогда не называл себя любителем природы.{w} Но сейчас я невольно наслаждался почти нереальной красотой моего окружения."
    "Солнце согревало моё бледное лицо, шелест листьев приятно успокаивал. Я испытал сильное желание просто лежать и смотреть, как по кристально-голубому небу лениво плывут облака."
    "Однако я не мог себе позволить этой роскоши."
    th "Что мне делать?"
    "Мобильник не ловил связь, и позвонить я никуда не мог."
    "Страх с новой силой захлестнул меня."
    th "Эти линии электропередач... по ним я смогу выйти к людям."
    "Хотя кто знает, как долго меня хватит идти по дороге вдаль? И кто знает, когда встретится населенный пункт?"
    "А ведь меня будут преследовать. Слишком много сил они потратили, чтобы устроить похищение!"
    "Я вздохнул. И вот я снова вернулся к первоначальному вопросу."
    "Зачем?{w} Зачем я вообще кому-то сдался?"
    th "До сегодняшнего дня у меня было предостаточно вопросов без ответа.{w} Не хватало ещё прибавлять к ним новые."
    "Я медленно направился назад к автобусу."
    "Страх ушёл, осталась лишь какая-то безразличная решимость. Много ли мне терять?"


    "..."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_camp_entrance_day 
    with dissolve2


    window show
    "За автобусом пролегала виденная мной ранее кирпичная стена."
    "Над воротами красовалась нелепая надпись «Совёнок»."
    "По бокам возвышались статуи, в которых легко можно было угадать пионеров."
    me "Заброшенный пионерский лагерь. Просто здорово."
    "По моей спине пробежал холодок."
    "В памяти проносились все виденные мной ужастики и прочитанные истории."
    th "Возможно, возвращаться сюда было не лучшим выбором."
    "Однако это место совсем не выглядело таким уж заброшенным: ржавчина на воротах была закрашена в несколько слоёв, гипс статуй бел и гладок."
    me "«Совёнок»..."
    "Рядом со статуей пионерки у опоры для проводов я заметил столбик с дорожныыми знаками.{w} Первый означал, что здесь находятся дети, а второй показывал остановку автобуса с маршрутом №410."
    th "Автобус номер четыреста десять..."
    th "Такое ощущение, будто я забыл что-то очень важное..."
    "Я, невольно стараясь ступать тише, направился к воротам."
    "Мне оставалось сделать последние пару шагов, как вдруг ворота приоткрылись, и мне навстречу вышла{w} девочка."
    show sl normal pioneer far at center    with dissolve
    "Все мои силы ушли на то, чтобы не крикнуть при неожиданном появлении незнакомки, так что теперь я стоял, не смея пошевелить и рукой."
    "Девчонка лет четырнадцати. Длинные светлые волосы, заплетённые в косы...{w} пионерская форма."
    th "Значит, пункт населён, и лагерь действует. Но пионерская форма в наше время?"
    th "Ну, по крайней мере, некоторая извращенная логика здесь присутствует."
    
    show sl smile pioneer at center   with dissolve
    "Незнакомка подошла ближе. Я невольно отметил великолепие её голубых глаз."
    sl "Привет! Ты, наверное, только что приехал?"
    window hide

    menu:
        "Не отвечать ей":
            window show
            "Я настороженно наблюдал за девчонкой."
            "От нее не исходило явной угрозы, но дураку понятно, что все здесь не так просто."
            th "Не стоит совершать необдуманных действий в этой ситуации."
            show sl normal pioneer at center   with dspr
            sl "Чего молчишь?"
            "Я как можно непринужденнее ответил:"
            me "Ну... да..."
        "Ответить":
            $ lp_sl = (lp_sl) + (1)
            window show
            me "Кхм, да, только что..."

    show sl smile2 pioneer at center   with dspr
    sl "Что же, добро пожаловать!"
    "Она широко улыбнулась."
    "Пока все шло неплохо..."
    th "Или не стоило возвращаться? Надо было идти вдоль ЛЭП."
    "Я продолжал глупо смотреть на свою собеседницу. Титаническими усилиями я попытался выдавить из себя дружелюбную улыбку."
    show sl normal pioneer at center   with dspr
    sl "Что смешного?"
    "Девочка окинула меня взглядом с ног до головы."
    "Я вздрогнул и невольно отступил на шаг назад."
    me "Н... ничего..."
    show sl smile pioneer at center   with dspr
    sl "Ну и славно."
    th "Сомневаюсь. Но пора что-то предпринимать."
    me "А ты знаешь..."
    show sl normal pioneer at center   with dspr
    sl "Как выгрузишь свои вещи, заглягни сразу к вожатой, она всё тебе расскажет!"
    sl "Смотри.{w} Сейчас идешь прямо-прямо, доходишь до площади, затем сворачиваешь налево, дальше будут домики."
    "Она показала в сторону ворот, как будто я знал, что за ними."
    sl "Ну, спросишь у кого-нибудь, где домик Ольги Дмитриевны!"
    me "Я... э-э-э..."
    sl "Надеюсь, всё понял?"
    "Мои мысли путались."
    sl "А мне пора."
    hide sl  with dissolve
    "Девочка помахала мне рукой и скрылась за воротами."
    th "Вожатая, да?"
    th "Меня всё меньше радует происходящее."
    th "Итак, вперёд, навстречу ответам!"

    stop ambience fadeout 2

    "Я побрёл в указанном направлении."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_clubs_day 
    with dissolve

    play ambience ambience_camp_center_day fadein 5

    window show
    "Метров через пятьдесят я наткнулся на небольшое одноэтажное здание.{w} Вывеска рядом с дверью гласила «Клубы»."
    "Ниже что-то было подписано неразборчивым почерком."

    stop music fadeout 5

    "Я уже было собирался подойти поближе..."

    play sound sfx_open_door_clubs fadein 0

    show un normal pioneer far at left   with dissolve   
    "Как дверь открылась, оттуда вышла ещё одна девочка в пионерской форме."
    "Мне сразу бросилось в глаза её невероятно грустное лицо. Весьма симпатичное."
    "Девочка остановилась, неподвижно глядя в мою сторону."
    show un surprise pioneer far at left   with dspr
    "Я застыл на месте, думая, как лучше поступить."

    play music music_list["i_want_to_play"] fadein 5

    show us grin sport far at right   with dissolve 

    play sound sfx_bush_leaves fadein 0

    "Неожиданно из соседних кустов выскочила очередная девчонка. На этот раз совсем мелкая."
    "На её ярко-красной майке резко выделялась надпись жёлтыми буквами «СССР»."
    th "У одной из них наверняка можно спросить, где находится Ольга Дмитриевна."
    show un surprise pioneer far at left   with dspr
    show us surp3 sport far at right  with dspr:
        linear 1.0 xalign 0.28
    "Тем временем девчонка в красной майке подскочила к грустной девочке и, энергично жестикулируя, начала что-то бойко той рассказывать."
    show un shy pioneer far at left   with dspr
    "Та явно смутилась и, ничего не ответив, потупила взгляд."
    "Я, до того тактично стоявший в стороне, решил уж было приблизиться, как вдруг мелкая достала что-то из кармана и начала трясти этим перед лицом своей подруги."
    window hide

    scene cg d1_grasshopper 
    with dissolve

    window show
    un "Ииииии-иии-иииииии!"
    "По внешнему виду нельзя было и предположить, что голос этой девочки может быть настолько громким."
    "Пронзительный вопль затих вдали, когда его обладательница скрылась за поворотом."

    $ persistent.sprite_time = 'day'
    scene bg ext_clubs_day 
    show us grin sport at left  with dspr:
        linear 2.0 xalign 2.0
    with dissolve

    "Мелкая посмотрела на меня, хитро улыбнулась и побежала за ней."
    hide us 
    th "Они выглядят... обычно?"
    th "Не знаю, что я собирался увидеть внутри лагеря, но явно не {i}это{/i}..."

    stop music fadeout 5

    "Мне ничего не оставалось, кроме как дальше идти наугад."
    th "Факт есть факт - на данный момент я не могу объяснить происходящее."
    "Я бросил последний взгляд на здание."
   
    th "Не похоже, чтобы там еще кто-то был..."
    "На свежем воздухе я чувствовал себя в относительной безопасности, так что до поры до времени я постараюсь избегать неизвестных зданий."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_houses_day 
    with dissolve

    window show
    "Я продолжил свой путь в направлении, которое давно забыл."
    "По обе стороны дорожки расположились домики местных обитателей.{w} Не могу сказать, был ли кто-то внутри."
    "Неожиданно, но все окружение выглядело таким... уютным. Я никогда не был пионером, так что мне не с чем сравнить первые впечатления. Но представлял я пионерлагерь несколько по-другому."
    "Сильно прогнившие деревянные бараки, внутри - скрипучие и ржавые койки. Глупые, взятые с потолка правила. Сигареты как валюта. Подъем в шесть утра, построение..."
    th "Кхм, возможно, я ошибаюсь.{w} Следов разрухи тут совсем нет, наоборот, всё новое. А какие порядки здесь царят, ещё предстоит выяснить."

    play sound sfx_punch_medium

    with vpunch
    stop ambience fadeout 30
    "В ту же секунду я получил сильный удар в спину."
    "Достаточно сильный, чтобы ощутимо пошатнуть меня, но я устоял на ногах."
    th "Началось! Я так и знал!"
    me "Какого чёрта?!"
    "Вся моя уверенность вмиг улетучилась. Ноги стали ватными и подкашивались. Кое-как я заставил себя обернуться."
    show dv smile pioneer2 at center   with dissolve
    $ volume(0.5, 'music')
    play music music_list["awakening_power"] fadein 10
    "Опять девочка."
    "С широко распахнутым ртом я уставился на новую знакомую."
    "Ожидаемая пионерская форма, хотя и сидящая на этот раз... вызывающе.{w} Рыжие волосы и необычная причёска привлекали не меньше внимания."
    "Я мог бы назвать эту девчонку настоящей красавицей, если б не явно читавшаяся в её глазах злоба."
    show dv grin pioneer2 at center   with dspr
    dv "Челюсть с пола подними."
    dv "Новенький, значит?"
    me "Что?"
    dv "На три дня опоздал в лагерь, надо же!"
    show dv laugh pioneer2 at center   with dspr
    dv "До Северного полюса не доехал?"
    "Я сообразил, что до сих пор в зимней одежде. На улице жара, но я, озадачившийся своей проблемой, совсем забыл снять пальто и джемпер."
    show dv grin pioneer2 at center   with dspr
    "Она вдруг крикнула кому-то за моей спиной:"
    dv "Эй, идите сюда все! У нас новенький!"
    "Я сдержал поток слов, готовых сорваться с языка.{w} Шестым чувством я заподозрил что-то неладное. Совсем не хочу сейчас оказаться в центре внимания среди кучи неизвестных мне ребят."
    me "Мне надо идти."
    stop music fadeout 4
    stop ambience
    "Надеюсь, это прозвучало с уверенностью."
    hide dv  with dissolve
    "Я уже обошёл её, чтобы пойти дальше..."
    play sound sfx_punch_medium

    with vpunch
    play music music_list["doomed_to_be_defeated"] fadein 3
    $ volume(1.0, 'music')
    "Она ловко поймала меня, зажав локтем шею так, что я даже вздохнуть не мог, не то что позвать на помощь, даже слова сказать!"
    th "Да куда я попал?!"
    
    
    "Я попытался бороться, но держала девчонка крепко, и у меня никак не получалось вывернуться."
    "К нам начали стекаться пионеры с разных сторон. Они появлялись из ниоткуда, как ниндзя, сидевшие в засаде: в кустах, за стволами деревьев и домиками."
    "Все в формах с красными галстуками."
    th "Кажется, мне хотят помочь!"
    "Чёрт, как же наивно было так думать!"
    me "Помо...{w} Кха!"
    th "Надежда умирает последней..."
    "Меня явно никто не услышал. Или не хотел слышать."
    dv "Эй, народ! Несите-ка сюда по ведру за каждый пропущенный день! Сейчас нальём ему штрафных за шиворот!"
    "Пионеры что-то радостно закричали, где-то сбоку показалась троица неизвестных парнишек. Они тащили три полных ведра, от спешки чуть расплёскивая воду."
    "Лиц пионеров я не различал - всё расплылось перед глазами. Эта рыжая бандитка предусердствовала с нажимом на мою бедную шею."
    dv "Быстрее, Марк, Даня! Надо облить новенького, новенький сам себя не обольёт!" #TODO Марку(от имени Marduke прототипа) Токареву и Дане (от прототипа Мардука - Dengokuman из тайтла Бо-бо-бо) Лирическому нужны спрайты и атрибуты персонажей. Вопрос на обсуждение. 
    "Никто не жаждал помочь, напротив, все только хотели принять участие в этом унижении новоприбывшего!{w} Я всеми фибрами души будто ощущал, как эта масса безликих мучителей смеётся надо мной..."
    "«Какой же он жалкий!» - наперебой шептались голоса.{w} «Облить и наподдать ему - святое дело!» - вторили им другие. «Хи-хи-хи!» - слышится гадкий смех девчонок..."
    th "Это какой-то заговор! Чёрт!"
    "Вдруг я сообразил, что незнакомка невовремя ослабила хватку. Но вовремя - для меня!"
    menu:
        "Попытаться бежать":
            window show
            "Я сделал то, что первым пришло в голову: попытался вывернуться ещё раз."
            th "Есть!"
            "Но рыжая поймала меня за пальто на первом же шагу к свободе и повалила на пятую точку."
            me "Ай!"
            dv "А ну стой, паразит! Четвёртого ведра захотел?"
            "Ватага пионеров с мерзким улюлюканьем обступила меня, чтобы не дать мне смыться. Перекрыли все пути!"
            th "Ну всё, сейчас будет подмочена моя нулевая репутация в этом новом коллективе..."
            with vpunch
            show blink
            "В ту же секунду меня окатило холодной водой...{w} Волосы слепило, я зажмурил глаза, ожидая, что непрошеные слёзы не заметят из-за мокрого лица."
            dv "Первая штрафная! Теперь вторую..."
            "Я съёжился и принял на себя очередной поток из ведра."
            th "Чёрт..."
            "Я начал отплёвываться от попавшей в рот воды и закашлялся - случайно вдохнул её."
            hide blink
            show unblink
            "Не просто вдохнул, а хлебнул.{w} Горя и сполна, как говорится."
            hide unblink
            dv "Давайте подождём немного, растянем наказание!"
            show blink
            th "Какая вредная сука!"
            "В сердцах подумал я, и ничуть не жалею.{w} Хотя нынешние девчонки бывают хуже. Гораздо хуже..."
            "С такими невесёлыми размышлениями я пропустил последнюю штрафную, словно и не заметил её."
            "Вода замочила всю одежду, и я промок до нитки. Футболка неприятно слиплась на теле."
            "Мне позволили подняться самому, и от этого мне стало ещё гаже."
            stop music fadeout 7
            hide blink
            show unblink
            "В сыром пальто я стоял как упакованный в бесформенный холодный куль."
            hide unblink
            play ambience ambience_camp_center_day fadein 5
            th "Чёрт, куда же идти теперь? А, к вожатой..."
            "Пионеров и след простыл: все разбежались с аллейки, как будто никого тут и не было.{w} Остался один я стоять на широком чёрном пятне, впитавшемся в дорожку."
            th "Та девочка сказала повернуть налево, кажется..."
            "И я пошёл, сняв пальто, чтобы выжать из него воду."
            jump mt_pioneer_form
        "Попытаться забороть":
            "Наверно, я был сумасшедшим."
            "С этой девчонкой никак не справиться так легко, нужно брать хитростью."
            "Забороть - и о чём я только думаю?"
            "Еле скосив глаза, я резко схватил её за мягкую округлость, подцепив края рубашки и лифчика, и сильно дёрнул, являя всем пионерам на свет смуглую и нежную девичью грудь."
            dv "Ох-х!!!"
            "С глубоким возмущением выдохнула она, тут же прикрываясь, и в этот момент я с безумным смешком, если не хохотом, выкрутился из хватки и косо, как ракета Протон-М, рванул в сторону, избегая пионеров."
            me "У-ха-ха!"
            "Моё бегство сопроводил женский крик, кажется, со всхлипом в голосе, но я не прислушивался. В ушах засвистел ветер."
            th "Бежать отсюда скорее!"
            
            $ persistent.sprite_time = 'day'
            scene bg ext_square_day 
            with dissolve

            window show
            "Теперь я оказался на площади. Не мешкая ни секунды, я мигом пересёк её."
        
            $ persistent.sprite_time = 'day'
            scene bg ext_path_day 
            with dissolve
        
            "Адреналин ударил мне в голову, и я ускорился. Никакая одышка не остановит меня! Лучше влететь в сосну и сдохнуть, чем даться в руки этим обезьянам!"
            th "А ведь я заборол её! Просто очень грязным приёмом!"
            th "Пусть считает себя наказанной за «непристойный» вид."
            "Не знаю, преследовали меня или нет, но я наверняка убежал очень далеко и сиганул в первые увиденные пышные кусты в пролеске."
            stop music fadeout 5
            play ambience ambience_camp_center_day fadein 6
            th "Здесь можно отсидеться, здесь можно отсидеться..."
            "Мои мысли путались в такт сбивчивому дыханию, и я согнулся в три погибели, скрывшись за ветвями малинника."
            th "Хорошо, что я не закончил побег так же, как Протон-М свой полёт."
        
            "Теперь я с облегчением вздохнул, не теряя бдительности. Со стороны дороги ещё могли показаться пионеры, во главе с рыжей, жаждущие отмщения."
            "Отомстит она или нет - неважно, я просто вышел победителем и унизил её раньше, чем она меня." #TODO Какой-нибудь маркер, чтобы события этой ветки учитывались в вариантах диалогов с Алисой.
            "Минут десять никто на дороге не появился. Кажется, дерзкого новичка решили не искать, мол, отыгрался."
            "Я с опаской вылез из кустов и побрёл в противоположную от лагеря сторону."
            "Ещё бы я туда пошёл! Мой инстинкт сохранения пока цел, а внутренний детектор сигналит о девчонке, которой я стал врагом."
            "Я не хотел признавать, что боюсь девочки младше меня лет на десять. Однако если подумать, я намного старше и могу позволить себе бравировать с малолетками как захочу."
            th "Хм, всё чревато тёмной, когда в отместку начинают лупить спящего, как в армии. Я видел это в  «Цельнометаллической оболочке»."
            th "Погодите, а с какой стати я буду тут ночевать? Ну уж нет. Шиш вам."
            "Ноги сами вели меня куда-то вперёд..."
            jump boathouse_meeting

label boathouse_meeting:

    $ persistent.sprite_time = 'day'
    scene bg ext_boathouse_day 
    with dissolve

    play ambience ambience_boat_station_day fadein 5

    window show
    "Я вышел на пристань."
    th "Везучий, да."
    "Я уже собирался повернуть назад, как..."
    sl "Ой, так тебе не сюда!"

    play music music_list["take_me_beautifully"] fadein 5

    show sl smile swim at center   with dissolve
    "Передо мной стояла светловолосая девочка, которую я встретил самой первой."
    sl "Я же сказала повернуть налево на площади, а ты куда пошел?"
    "Пионерская форма на ней сменилась на купальник. Я сглотнул."
    sl "Ой, я же так и не представилась!{w} Меня Славя зовут!"
    sl "Вообще, полное имя Славяна, но все зовут Славей."

    $ meet('sl', u"Славя")

    me "А... да..."
    "Её купальник несколько мешал обстановке. Я не мог собраться с мыслями."
    sl "А тебя?"
    "Казалось, её взгляд пронизывает меня насквозь."
    me "А... я... да... Семён..."
    show sl smile2 swim at center   with dspr
    sl "Очень приятно, Семён."
    sl "Ладно, я уже заканчиваю.{w} Ты подожди меня тут минутку, я сейчас переоденусь, и вместе пойдём к Ольге Дмитриевне, хорошо?"
    me "Хорошо..."
    hide sl  with dissolve
    "В ожидании я прошёл до конца причала по деревянным понтонам и облокотился на перила."
    th "Я уже не так боялся, в конце концов, это не слишком похоже на похищение."
    th "Может, меня с кем-то перепутали?"
    th "Наверняка это просто глупое недоразумение.{w} Надеюсь, никто не будет слишком злиться, когда оно раскроется."
    "Что здесь было особенно приятно, так это наслаждаться летним пейзажем."
    "Слабый ветерок, искрящаяся бликами вода, огромные белые, как пух, облака."
    "Посреди озера через продолговатый остров тянулась железная дорога.{w} По ней тут же промчался небольшой состав, точно опережая мою мысль."
    th "Стоп, это значительно упрощает дело. Если что, я смогу легко убраться отсюда на поезде или хотя бы дойти до станции на путях."
    sl "Пошли?"
    "Я обернулся."
    show sl smile pioneer at center   with dissolve
    "Рядом со мной стояла Славя, вновь одетая в пионерскую форму."
    me "Пошли..."
    "Почему-то эта девочка казалась мне очень... надёжной."
    
    stop ambience fadeout 4

    window hide
    
    jump sl_mt_house
    
label mt_pioneer_form:
    $ persistent.sprite_time = 'day'
    scene bg ext_square_day 
    with dissolve
    
    "По дороге я свернул на площадь."
    "Тишина, никого нет.{w} Меня переполняла глубокая обида."
    th "Хоть бы кто встретился и подсказал, где эта Ольга Дмитриевна!"
    "К счастью, у памятника неизвестному мне товарищу поднялась чья-то фигура, которую я сразу не приметил сбоку."
    show ug normal at center   with dissolve 
    "Парень неторопливо приблизился ко мне."
    "Он сутулился, заложив руки в карманы брюк."
    me "Эй! Как найти Ольгу Дмитриевну?"
    "Он словно пропустил вопрос мимо ушей.{w} Может, они ещё и бойкот решили организовать?"
    ug "Ты тот самый новенький?"
    me "Да, наверно..."
    ug "Значит, всё-таки облили.{w} Как же меня достал их ежедневный шум. А ещё пара недель впереди... С ума сойти."
    me "Так ты не с ними?"
    "Он поморщился так, будто учуял нечто дурно пахнущее."
    ug "Чтоб я с ними водился? Фи. В следующий раз думай, что говоришь."
    me "Ладно-ладно... Подскажи, где домик вожатой.{w} Пожалуйста."
    ug "Иди до последней улицы домиков, она живёт в самом крайнем справа."
    me "Спасибо."
    ug "Не за что."
    "Буркнул он."
    "Я пошёл вперёд, как вдруг повернулся к нежданному спасителю ситуации."
    me "А как тебя зовут?"
    ug "Никак."
    "Он ни разу не изменил своего хмурого выражения лица, исполненного презрения и мрачностью ко всему окружающему."
    
    $ meet('ug', u"Угрюмый") #TODO Нарисовать спрайты Угрюм-куна, можно с закосом под Хисао, как скидывал анон в треде. Прописать персонажа в ресурсных файлах.
    
    "Значит, будет по прозвищу Угрюмый."
    ug "Если её не будет дома, иди в административный центр, это слева от памятника."
    "Бросил он мне вслед."
    me "Спасибо!"
    hide ug  with dissolve
    th "Хоть кто-то адекватный среди всех. Может, ещё та светловолосая девочка, но тут они для меня все как-то одинаковы. Почти."
    "Наконец, я нашёл дом вожатой."
    "Если от всего лагеря веяло уютом, то домик был самой его концентрацией. Пышные кусты сирени, обрамляющие его, довершали картину."
    "Одно из тех мест, где хотелось бы прожить до старости."
    "Рядом с маленькой лестницей к двери расположились приставленный к стене велосипед с проколотой шиной и складной лежак под тенью веток сирени."
    "Велосипед меня заинтересовал куда больше лежака, но сейчас было не до транспорта, надо понять, где я вообще и что дальше делать."
    "Я осторожно постучался в дверь."
    mt_voice "Открыто!"
    play sound sfx_open_dooor_campus_1 fadein 0
    "Я заглянул внутрь."
    $ persistent.sprite_time = 'day'
    scene bg int_house_of_mt_day #TODO cg: int_house_of_mt_noitem_day
    with dissolve

    play music music_list["smooth_machine"] fadein 5
    
    "Внутри домик выглядел именно так, как можно было предположить по фасаду."
    "Ничего особенного, но казалось, будто всё здесь прямо-таки кричит: «Уют!». Я отметил, что в плане порядка комнатка далеко ушла от моей квартиры."
    show mt normal pioneer far at center    with dissolve   
    "Возле кровати с разложенными вещами стояла девушка. Старше прочих."
    "Я бы дал ей лет двадцать пять."
    th "Эта фигура..."
    mt "Пришёл-таки!{w} Отлично!{w} Меня Ольга Дмитриевна зовут, я вожатая.{w} Ой, что с тобой?"
    show mt surprise pioneer at center   with dspr
    th "Этот голос..."

    $ meet('mt', u"Ольга Дмитриевна")
    
    me "А меня зовут Семён. Простите, я войти не смогу - наслежу ботинками. Меня облили пионеры ваши, за опоздание."
    show mt sad pioneer at center   with dspr
    mt "А зачинщицей, наверно, была Алиса, рыженькая такая?" # Развилка ответов?
    me "Да."
    "Вырвалось у меня."
    $ lp_mt = (lp_mt) + (1)
    show mt angry pioneer at center   with dspr
    mt "Ну, она у меня ещё дождётся!"
    show mt normal pioneer at center   with dspr
    "А что ты стоишь на пороге? Разувайся, не стесняйся! Всё уберём, если что.{w} Жаль, я не успела прибраться к твоему приходу."
    "Я скинул зимние ботинки и оставил их на улице под ступеньками, после чего прошёл в комнату."
    mt "Снимай всю мокрую одежду."
    me "Что?"
    mt "Снимай-снимай."
    "Успокаивающим голосом сказала она, намекая, что всё под контролем."
    "В это мало верилось, и я начал раздеваться."
    "Пока я стягивал сырые джемпер и футболку, Ольга Дмитриевна наспех, но аккуратно сгребла вещи с кровати в бумажный пакет и оставила его под окном."
    "Второпях она забыла убрать носок и бюстгальтер со спинок кровати, но повесила на дверцу шкафа плечики с сарафаном, прикрыв зеркало."
    mt "Какой у тебя рост, Семён?"
    "Она взяла с полочки аккуратно свёрнутое полотенце и дала его мне, чтобы я протёр мокрые волосы."
    me "..." #TODO Рост Семёна.
    mt "Ты подожди, я в административный центр схожу, найду тебе пионерскую форму по размеру."
    me "Спасибо..."
    hide mt  with dissolve
    "Сказал я, и Ольга Дмитриевна ушла."
    "Я изо всех сил взъерошил волосы, суша их полотенцем."
    th "Хорошая вожатая. Заботливая, если судить по первому впечатлению. Наверно, даже ровесница мне."
    "Я внимательней осмотрел комнату, прохаживаясь по ней в одних трусах."
    "Почти каждый предмет здесь находился на своём месте, с такой тщательной педантичностью всё было разложено и расставлено."
    th "Ох, Ольга Дмитриевна забыла свою сумку. Может, она и не планировала брать её..."
    "Я заглянул внутрь в надежде найти сотовый телефон. Если здесь вообще ловится антенна."
    "Не нашлось."
    th "Может, она носит его с собой..."
    "Я прекратил поиск и остановился."
    th "Почему вдруг все думают, что я один из приезжих? Как будто я сюда целенаправленно попал!"
    th "Какая-то непонятная чушь. За все школьные годы я ни разу не ездил с одноклассниками ни заграницу, ни в санатории."
    th "Я и сейчас не поехал бы в лагерь, даже забесплатно."
    "Время тянулось чертовски медленно."
    "От скуки я мельком заглянул во все незапертые тумбочки со шкафами и, не обнаружив ничего полезного, налил чаю из термоса."
    "Сев за столик, я отодвинул горшочек с комнатным растеньицем и принялся за самовольное чаепитие."
    scene bg black
    $ renpy.pause(2)
    $ persistent.sprite_time = 'day'
    scene bg int_house_of_mt_day 
    with dissolve
    "Пока я допивал вторую кружку, вернулась Ольга Дмитриевна."
    show mt normal pioneer far at center    with dissolve
    mt "Вот и всё! Держи форму."
    me "Извините, я тут чаю выпил."
    mt "Ой, не надо извинений."
    show mt smile pioneer at center   with dspr
    "Она мило уыбнулась."
    "Я с неохотой надел форму и опять сел за столик."
    show mt normal pioneer at center   with dspr
    me "А когда автобус следующий будет, а то я..."
    "Не подыскав слов, я указал куда-то в направлении автостоянки."
    show mt surprise pioneer at center   with dspr
    mt "А тебе зачем?"
    th "Возможно, нужно действовать тоньше."
    th "Узнаю ситуацию. Узнаю, как отсюда выбраться."
    me "Нет-нет, простое любопытство!"
    show mt normal pioneer at center   with dspr
    me "А где мы находимся?{w} Ну, в смысле, какой адрес?"
    me "Я родителям хочу письмо написать, что удачно добрался."
    mt "Так мне твои родители полтора часа назад звонили! Тебе привет передавали."
    th "Ага..."
    me "О, значит, я могу им позвонить! Я забыл сказать кое-что перед отъездом!"
    show mt smile pioneer at center   with dspr
    mt "Нет."
    "Она снова мило улыбнулась, и мне стало не по себе."
    me "Почему?"
    mt "А у нас телефона нет."
    me "..."
    me "Но мои родители звонили полтора часа назад?"
    mt "Я только из райцентра вернулась, там с ними и поговорила."
    th "Угу. Всё интереснее."
    me "А мне можно как-нибудь в этот райцентр?"
    mt "Нет."
    "Она всё так же улыбалась."
    me "Кхм... почему?"
    mt "Потому что следующий автобус будет только через неделю."
    "Как она тогда сумела съездить туда и обратно, я решил не спрашивать. Уже очевидно, что ответа я не получу."
    "В дверь постучались с улицы, и по разрешению Ольги Дмитриевны в домик вошла та голубоглазая девочка, с которой я встретился на стоянке."
    show sl normal pioneer at right   with dissolve
    sl "Рада видеть, что ты уже форме! А что у тебя с волосами?"
    me "Да так, неважно."
    "Не плакаться же ей в жилетку? В конце концов, это был стандартный лагерный розыгрыш, но пожёстче зубной пасты на лице, оставленной ночью шутниками на жертве."
    mt "Да это всё Алиса безобразничает."
    sl "Ясно. Ой, я же так и не представилась!{w} Меня Славя зовут!"
    
    $ meet('sl', u"Славя")
    
    me "Очень приятно. А я - Семён."
    mt "Ладненько, я побежала за Двачевской, а ты пока можешь осмотреть лагерь!{w} Вечером приходи на ужин, не забудь!"
    
    $ d1_dv_punishment = True
    $ d1_mt_good_relationship = True
        
    "С этими словами она покинула домик."
    hide mt  with dissolve
    show sl smile pioneer at right:
        linear 1.0 xalign 0.5
    "Я остался наедине со Славей."
    sl "Мне тоже пора – дела."
    sl "Ты походи, осмотрись.{w} Вечером увидимся."
    hide sl  with dissolve
    th "Ну после такого совсем нет желания куда-либо идти. Но на солнце голова быстрей высохнет, плюс удастся кого-то расспросить о лагере."
    menu:
        "Остаться в домике до вечера":
            jump day1_sitting_home
        "Прогуляться в лагере":
            jump day1_camp_walk

label sl_mt_house:

    $ persistent.sprite_time = 'day'
    scene bg ext_square_day 
    with dissolve

    window show
    "Вслед за Славей я вышел обратно на площадь."
    show dv smile pioneer2 far at left    with dissolve   
    show us grin sport far at right    with dissolve
    "Теперь по ней бегали «СССР» и девочка, организовавшая нападение на меня."
    "Я остановился, пытаясь сообразить, как снова не столкнуться с ней."
    show sl angry pioneer at center   with dissolve
    sl "Ульяна, хватит бегать! Я все Ольге Дмитриевне расскажу!"

    $ meet('us', u"Ульяна")

    show us laugh sport far at right   behind sl  with dspr
    show dv angry pioneer2 far at left   with dspr
    us "Есть, гржнинначаник!"
    "Алиса посмотрела на меня не то со злобой, не то с обидой, но даже не заговорила."
    "Обе девочки убежали."
    hide dv 
    hide us 
    with dissolve
    show sl normal pioneer at center   with dspr
    "Я решил пока не задавать Славе лишних вопросов."
    th "Дождусь сперва встречи с этой вожатой."
    th "Ольга Дмитриевна, верно?"
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_houses_day 
    with dissolve

    stop music fadeout 5

    play ambience ambience_camp_center_day fadein 5

    window show
    "Пройдя через ряды жилых бараков, мы остановились перед небольшим одноэтажным домиком."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_house_of_mt_day 
    with dissolve

    window show
    "Если от всего лагеря веяло уютом, то домик был самой его концентрацией. Пышные кусты сирени, обрамляющие его, довершали картину."
    "Одно из тех мест, где хотелось бы прожить до старости."

    show sl normal pioneer at center   with dissolve
    sl "Ну что, пошли?"
    mt_voice "...и хватит издеваться над Леной!.."
    
    play sound sfx_open_dooor_campus_1 fadein 0

    show us grin sport far at left   behind sl  with dissolve   

    "Внезапно из домика выскочила Ульяна и, все так же хитро скалясь, унеслась в неизвестном направлении."
    hide us  with dissolve
    show un normal pioneer far at left   behind sl  with dissolve   
    "За ней я увидел ту грустную девочку. Бедняжка выглядела ещё печальнее, чем прежде."
    sl "Лена, не обижайся ты на неё!"

    $ meet('un', u"Лена")

    th "Значит, Лена? Годы в наушниках не пощадили мой слух."
    show un shy pioneer at left  behind sl  with dissolve
    un "Да я не..."
    "Она покраснела, и, потупив взгляд, прошла мимо меня."
    hide un  with dissolve
    "Я невольно обернулся, но Славя сказала:"

    stop ambience fadeout 3

    sl "Пойдем."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg int_house_of_mt_day #TODO cg: int_house_of_mt_noitem_day
    with dissolve

    play music music_list["smooth_machine"] fadein 5

    window show
    "Внутри домик выглядел именно так, как можно было предположить по фасаду."
    "Ничего особенного, но казалось, будто всё здесь прямо-таки кричит: «Уют!». Я отметил, что в плане порядка комнатка недалеко ушла от моей квартиры."
    show mt normal pioneer far at center    with dissolve   
    "Возле окна стояла девушка. Старше прочих."
    "Я бы дал ей лет двадцать пять."
    th "Эта фигура..."
    mt "Пришёл-таки!{w} Отлично!{w} Меня Ольга Дмитриевна зовут, я вожатая."
    th "Этот голос..."

    $ meet('mt', u"Ольга Дмитриевна")

    me "Очень приятно, я - Семён."
    show mt normal pioneer at center   with dissolve
    "Она подошла ближе. Слишком близко."
    mt "Мы тебя с утра ждём."
    me "Ждёте? Меня?.."
    show mt smile pioneer at center   with dspr
    mt "Да, конечно!"
    me "А когда автобус следующий будет, а то я..."
    "Не подыскав слов, я указал куда-то в направлении автостоянки."
    show mt surprise pioneer at center   with dspr
    mt "А тебе зачем?"
    th "Возможно, нужно действовать тоньше."
    th "Узнаю ситуацию. Узнаю, как отсюда выбраться."
    me "Нет-нет, простое любопытство!"
    me "А где мы находимся?{w} Ну, в смысле, какой адрес?"
    me "Я родителям хочу письмо написать, что удачно добрался."
    show mt normal pioneer at center   with dspr
    mt "Так мне твои родители полтора часа назад звонили! Тебе привет передавали."
    th "Ага..."
    me "О, значит, я могу им позвонить! Я забыл сказать кое-что перед отъездом!"
    show mt smile pioneer at center   with dspr
    mt "Нет."
    "Она мило улыбнулась."
    me "...{w} Почему?"
    mt "А у нас телефона нет."
    me "..."
    me "Но мои родители звонили полтора часа назад?"
    mt "Я только из райцентра вернулась, там с ними и поговорила."
    th "Угу. Всё интереснее."
    me "А мне можно как-нибудь в этот райцентр?"
    mt "Нет."
    "Она всё так же улыбалась."
    me "Кхм... почему?"
    mt "Потому что следующий автобус будет только через неделю."
    "Как она тогда сумела съездить туда и обратно, я решил не спрашивать. Уже очевидно, что ответа я не получу."
    show mt normal pioneer at center   with dspr
    show sl normal pioneer at right   with dissolve
    "Всё это время Славя стояла рядом со мной и, казалось, не находила в нашем диалоге ничего особенного."
    show mt surprise pioneer at center   with dspr
    mt "Ой, надо тебе форму подобрать! Какой у тебя рост? Я найду тебе подходящую."
    "Последнее, что я хотел - это наряжаться в пионерские шортики и завязывать нелепый красный галстук.{w} Я прямо почувствовал гнилой запах призрака коммунизма, воспарившего надо мной.{w} Но идея продолжать ходить в зимней одежде мне также не импонировала."
    me "Да, спасибо. Мой рост ..." #TODO Настоящий рост Семёна, не того пионера, в чьём он теле.
    show mt normal pioneer at center   with dspr
    th "Я один считаю, что ходить в такую погоду в зимнем плаще и ботинках - странновато?{w} Один, да...{w} Придётся надеть форму, когда выдадут."
    show mt smile pioneer at center   with dspr
    mt "Ладненько, я побежала, а ты пока можешь осмотреть лагерь!{w} Вечером приходи на ужин, не забудь!"
    hide mt  with dissolve
    "С этими словами она покинула домик."
    show sl smile pioneer at right  with dspr:
        linear 1.0 xalign 0.5
    "Я остался наедине со Славей."
    sl "Мне тоже пора – дела."
    sl "Ты походи, осмотрись.{w} Вечером увидимся."
    hide sl  with dissolve
    th "Факт есть факт - ответов мне давать никто не собирается. Ладно, поживём - увидим."
    "С нервным смешком я отправился на осмотр территории."
    
    stop music fadeout 5
    
    jump day1_camp_walk

label day1_camp_walk:

    th "Возможно, стоит попытаться расспросить кого-то ещё."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_houses_day 
    with dissolve

    play ambience ambience_camp_center_day fadein 5

    window show
    "Проходя мимо домиков, я не без удивления заметил идущего навстречу мне пионера."
    show el normal pioneer far at center    with dissolve
    "Он сразу остановил меня набившими оскомину вопросов:"
    
    stop ambience fadeout 1

    play music music_list["eat_some_trouble"] fadein 2

    el "Привет, ты новенький? Семён, верно?"
    me "Да, а ты откуда?.."
    show el grin pioneer at center   with dissolve 
    el "Да уже все знают! Я, кстати, Электроник. Настоящий. Можешь так меня и звать."

    $ meet('el', u"Электроник")

    th "Электроник. Настоящий. Великолепно...{w} Это что, шутка? Кличка? А ведь похож..."
    me "М-м, ясно..."
    show el smile pioneer at center   with dspr
    el "Ульянка меня еще Сыроежкой зовет."
    me "Сыроежкой?"
    el "Фамилия у меня такая – Сыроежкин."
    me "М-м, ясно..."
    el "Давай я тебе тут всё покажу!"
    "Определённые плюсы в его предложении были."
    me "Давай, буду рад."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_square_day 
    with dissolve

    window show
    "И вновь я оказался на площади."
    "На одной из лавочек сидела Лена и читала какую-то книжку.{w} Электроник уверенным шагом подошёл к ней."
    show el normal pioneer at cleft   with dspr
    show un normal pioneer at cright   with dspr
    with dissolve
    el "Лена, привет! Это новенький, Семён, знакомься!"
    "Бойко начал он."
    me "Привет.{w} Ну, мы уже заочно знакомы."
    show un shy pioneer at cright   with dspr
    un "Да..."
    "Она на секунду оторвала глаза от книжки, посмотрела на меня, покраснела и вновь принялась за чтение, стараясь не замечать наше присутствие."
    el "Ладно, пойдём дальше."
    "Я опешил от такого «знакомства», однако, принимая во внимание неуемную активность Электроника и явно бросающуюся в глаза скромность Лены...{w} Вероятно, по-другому бы не получилось."
    me "Пошли."
    window hide

    stop music fadeout 5

    $ persistent.sprite_time = 'day'
    scene bg ext_dining_hall_away_day 
    with dissolve

    window show
    "Мы вышли к очередному строению, в котором я тут же опознал столовую."
    show el normal pioneer at center   with dissolve
    el "А вот это..."
    me "Столовая, угадал?"
    show el smile pioneer at center   with dspr
    show el normal pioneer at center   with dspr  #Интересный ход. Надо попросить Лолбота озвучить.
    show dv normal pioneer2 far at left    with dissolve   
    "На крыльце столовой околачивалась та самая Алиса." #А откуда он знает её имя, если ему его до этого Ольга Дмитриевна не говорила?
    "Не тот человек, которого я сейчас хотел бы видеть."
    show el scared pioneer at center   with dspr
    el "Алиса Двачевская. Всегда в плохом духе."

    $ meet('dv', u"Алиса")

    "Он говорил шёпотом, и я понимал, почему."

    stop ambience fadeout 2

    el "А назовешь ее ДваЧе, так вообще закипит!"

    play music music_list["awakening_power"] fadein 2

    show dv angry pioneer2 far at left   with dspr
    dv "Ты что-то сказал?!"
    "Невероятно, но она услышала."
    "В мгновение ока Алиса спрыгнула с крыльца и метнулась в нашу сторону."
    show el shocked pioneer at center   with dspr
    el "Ну... э-э... Ладно, дальше ты сам как-нибудь..."
    "Пробормотав еще что-то несвязное, Электроник бросился бежать."
    hide el  with dissolve
    window hide

    menu:

        "Побежать за ним":
            stop music fadeout 3

            window show
            "Решив, что сейчас не самое подходящее время для разговора с Алисой, я рванул вслед за ним."
            window hide

            $ persistent.sprite_time = 'day'
            scene bg ext_square_day 
            with dissolve

            play ambience ambience_camp_center_day fadein 5

            window show
            "Выбежав на площадь, я понял, что потерял Электроника из виду."
            "Двачевской также здесь не было."
            "Сев на ближайшую лавку отдышаться, я некоторое время оправдывался перед собой."
            th "Ну девочка... Ну агрессивная...{w} Но зачем бежать-то было?.. Я же старше."

            stop ambience fadeout 2

        "Ничего не делать":
            show dv angry pioneer2 at left   with dissolve
            window show
            "Поравнявшись со мной, она бросила на меня злобный взгляд:"
            if d1_dv_punishment:
                dv "Что, наябедничал и доволен?"
                window hide
                menu:
                    "Сказать правду":
                        window show
                        me "Вожатая сразу догадалась, что это ты, а я всего лишь подтвердил. Не стоило обливать меня, я злопамятный."
                        dv "Ишь, какой недотрога выискался! Чуть что, сразу за юбку прячешься?"
                        me "Ты своего добилась, чего ещё надо?{w} Если считаешь, что я не могу постоять за себя, то вспомни, сколько вас там набежало на одного. Сама удрала бы?"
                        init:
                            $ d1_me_braveness_raised = True #TODO Для будущего ивента, где Семён заступается за Алису перед местными гопниками, за это будет итоговый лавпойнт на балл выше. Почему так: Алиса наглядно видит, что Семён изменился, эмоциональный отклик на это выше. Навеяно воспоминаниями анонов из тредов. В ивенте с карточной игрой тоже надо.
                        me "Может, ты хочешь реванш с моей стороны? Валяй, придумай что-нибудь, где мы сойдёмся один на один, только потом не плачь, если проиграешь."
                        "Я мысленно одёрнул себя за самонадеянность, но слово не воробей.{w} Всё это было сущим ребячеством, и я удивился, насколько же низко пал, чтобы вести детскую игровую войну с какой-то девчонкой."
                        "Ситуация будто стала отголоском детства, когда во дворе я с пацанами вёл себя так же. Для нас это было естественной формой общения."
                        "Я с неприятным чувством понял, что психически совсем не вырос. Может, поэтому мне с людьми помладше легче и удобней общаться?"
                        show dv normal pioneer2 at left   with dissolve
                        dv "Как два пальца в розетку. Не сомневайся, плакать будешь только ты."
                        "Она круто развернулась и ушла в столовую."
                        hide dv  with dissolve
                        th "Как быстро она перестала злиться, сразу после возникшей перспективы снова показать своё превосходство...{w} Нет, всё прямо как в детстве."
                    "Соврать":
                        init:
                            $ d1_kr_hate_raised = True #TODO Этот ответ прибавит шанс на бэд-энд с Криппи-тян. Ложь действует на личность Алисы в виде Криппи как стресс. Навеяно анимцой Бакемоногатари.
                        window show
                        me "Да ничего я не ябедничал. И после того я с тобой говорить даже не хочу."
                        show dv sad pioneer2 at left   with dissolve
                        "На её лице проступило разочарование."
                        dv "Да ты... ты... Трус, вот ты кто."
                        hide dv  with dissolve
                        "Она ушла, оборвав разговор на печальной ноте."
                        "Не знаю, надо ли жалеть об этом. Наверно, мы просто не из тех, кто может легко ужиться между собой."
                        th "К чему мысли об этом?{w} Ни с кем уживаться я не собирался. Чем раньше свалю отсюда, тем лучше."
            dv "А с тобой я ещё поговорю!"
            me "М-м... Просто не забудь в следующий раз рубашку заправить, тогда, может быть, обосрамс не повторится."
            show dv shy pioneer2 at left   with dissolve
            show dv angry pioneer2 at left   with dissolve
            "Я усмехнулся, не без удовольствия заметив, как на мгновение Алиса стала пунцовой, то ли от смущения, то ли от гнева."
            dv "Это традиция такая, а ты всё испортил!"
            "Она капризничает что-ли?"
            "Я сделал лицо кирпичом и сухим тоном, как мой бывший лектор по экономике, сказал:"
            me "Я вообще враг традиций и шаблонов. Терпеть не могу, когда всё как у людей.{w} И коллективные издевательства не считаю чем-то хорошим."
            dv "Да причём тут издевательства? Ты в лагере никогда не был?"
            show dv surprise pioneer2 at left   with dissolve
            "Я, уперев руки в бока и склонив голову, с улыбкой многозначительно посмотрел на Алису, чтобы она без слов поняла мой ответ «Эх ты!»."
            me "Ну конечно не был!"
            "Всё же сказал я, не выдержав драматической паузы."
            show dv normal pioneer2 at left   with dissolve
            dv "Ой, а как я должна была догадаться?"
            "С насмешкой парировала она."
            "На этом моё остроумие иссякло."
            me "Не знаю..."
            dv "Незнание не овсобождает от обязанности! Если штрафовать новичка, то без исключений."
            me "Одну минуточку... Живое исключение стоит перед тобой."
            show dv shy pioneer2 at left   with dissolve
            show dv normal pioneer2 at left   with dissolve
            "Алиса снова вспыхнула, как только я напомнил о том происшествии."
            me "Максимум, на что я могу согласиться - это на купание в речке. Без штрафов, разумеется."
            init:
                $ d1_dv_mentioning_river = True #TODO После карточного ивента Алиса обязательно приходт на речку. В отместку тырит всю одежду Семёна, включая трусы, ибо нехер с ней связыватся!
            dv "Пф, какой же ты скучный! Хотя..."
            show dv smile pioneer2 at left   with dissolve
            "В её глазах зажёгся азарт, и она убежала, даже не попрощавшись."
            hide dv  with dissolve
            th "Что ещё задумала эта девка?"
            
            stop music fadeout 5

            hide dv  with dissolve

    "..." #TODO Нейтральный ивент. Возможно, свободное перемещение по карте.
    window hide

    with fade

    window show
    "Похоже, оставшееся время до ужина мне предстояло коротать в одиночестве."
    "Я неспеша побрёл в противоположную от столовой сторону."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_playground_day 
    with dissolve

    play ambience ambience_soccer_play_background fadeout 2

    play music music_list["went_fishing_caught_a_girl"] fadein 5

    window show
    "Спустя некоторое время, я оказался около футбольного поля."
    "Там вовсю шёл матч."
    "Я встал в сторонке и решил немного понаблюдать."
    "Когда-то давно, помнится, я весьма неплохо играл, пока не решил, что все это вряд ли стоит падений и ушибов."
    "Дети резво носились по полю. Особенно выделялась одна девочка."
    th "Девочка...{w} Ульяна!"
    th "Ну, этого стоило ожидать."
    "Я стоял достаточно далеко от поля, но Ульяна все же заметила меня."
    show us laugh sport far at center    with dissolve   
    us "Эй, ты!"
    us "Играть будешь?"
    "Я не знал, что ответить."
    "Я не питал особой любви к футболу. Но сейчас делать мне было решительно нечего."
    "Однако вряд ли то, что было на мне, можно было назвать подходящей одеждой."
    me "В другой раз!"
    "Я развернулся и пошёл назад."
    hide us  with dissolve

    stop music fadeout 5

    "Она что-то вопила мне вслед, но я сомневался в важности этого."
    window hide

    stop ambience fadeout 2

    scene bg black 
    with dissolve2

    play ambience ambience_camp_center_evening fadeout 2

    window show
    "..." #TODO Нейтральный ивент. Возможно, свободное перемещение по карте.
    "Жаркий день постепенно перетекал в не менее жаркий вечер." #TODO В прошедшем времени: Жаркий день уже перетёк в не менее жаркий вечер.
    "..."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_square_day 
    with dissolve2

    window show
    "В который раз за сегодня я обнаружил себя на площади."
    th "Нет плана. Нет информации. Нет еды."
    th "Вожатая, очевидно, что-то скрывает."
    th "Все остальные ведут себя предельно естественно, что, учитывая их возраст, позволяет с уверенностью предположить, что они здесь добровольно."
    th "Или так давно, что уже привыкли."
    th "Ждёт ли меня то же самое?"
    th "Ну, посмотрим. Сейчас основная моя задача - поесть."
    th "Завтра я с новыми силами приступлю к поиску ответов."

    play sound sfx_dinner_jingle_speaker

    "Из динамиков заиграла музыка."
    th "Сигнал к ужину, быть может? Мое почтение Павлову."
    "Я поспешил к столовой."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_dining_hall_away_day # С одной стороны, относительно светло для ужина. С другой - цг явно указывает на светлое время суток.
    with dissolve

    stop sound fadeout 2

    window show
    "На крыльце была Ольга Дмитриевна."
    show mt normal pioneer far at center    with dissolve   
    "..."
    show mt normal pioneer at center   with dissolve
    mt "Семён, чего стоишь? Проходи!"
    "Мой желудок явственно потребовал последовать ее совету."
    window hide

    stop ambience fadeout 3

    $ persistent.sprite_time = 'day'
    scene bg int_dining_hall_people_day 
    with dissolve

    play ambience ambience_dining_hall_full fadein 5

    window show
    "Мы вместе зашли внутрь."
    "Столовая представляла из себя...{w} столовую."
    "Одно время я частенько бывал в заводской столовой.{w} Эта была почти неотличима, разве что почище и поновее."
    "Ну, и контингент несколько другой."
    show mt normal pioneer at center   with dissolve
    mt "Семён, сейчас мы тебе место найдём."
    "Она окинула взглядом помещение."
    show mt rage pioneer at center   with dspr
    mt "Стой, Двачевская!"
    "Ольга Дмитриевна прикрикнула на проходящую мимо Алису."
    show dv normal pioneer2 at left   with dissolve
    dv "А что такое?"
    mt "Ты как одета?"
    dv "А как я одета?"
    "«Неплохо», - невольно подумал я."
    mt "Немедленно приведи в порядок форму!"
    show dv sad pioneer2 at left   with dspr
    dv "Ладно-ладно!"
    show dv normal pioneer at left   with dissolve
    "Алиса поправила рубашку и прошла мимо, косо глянув на меня."
    hide dv  with dissolve
    show mt normal pioneer at center   with dspr
    mt "Итак, куда бы нам тебя посадить?"
    "Она посмотрела и нашла свободное место."
    mt "Давай вот сюда, к Ульяне!"
    me "Это... А может..."
    show mt smile pioneer at center   with dspr
    mt "Да, точно, как раз уже и еда стоит!"
    "Мне не оставалось ничего другого, кроме как согласиться."
    "В конце концов, всё что мне хотелось - это поскорее поесть."
    hide mt  with dissolve
    show us normal pioneer at center   with dissolve
    us "Эй!"
    "Я повернулся."
    me "Чего тебе?"
    show us dontlike pioneer at center   with dspr
    us "Почему с нами в футбол не стал играть?"
    me "Одежда была не та."
    show us grin pioneer at center   with dspr
    us "А, ну тогда ладно, ешь."
    hide us  with dissolve

    play music music_list["i_want_to_play"] fadein 0

    "Я повернулся к своей тарелке и обнаружил, что котлета пропала."
    th "Дважды два равно четыре, параллельные линии не пересекаются, котлету спёрла Ульяна!"
    show us grin pioneer at center   with dissolve
    me "Отдай котлету!"
    us "В большой семье клювом не щёлкай!"
    me "Ах ты мелкая!"
    window hide

    menu:
        "Попытаться отнять котлету":
            $ lp_us = (lp_us) + (1)
            window show
            "Я грозно посмотрел на неё и уже было вытянул вперед руку..."
        "Ничего не делать":
            window show

    us "Нету у меня её, смотри!"
    "Действительно, тарелка Ульяны была пуста – похоже, она уже всё съела..."
    show us laugh pioneer at center   with dspr
    us "Ты не расстраивайся, сейчас что-нибудь придумаем!"
    hide us  with dissolve
    "Молниеносным движением она схватила мою тарелку и куда-то слиняла."
    "В один миг я лишился и котлеты, и гарнира. Моя жизнь - страдание."
    "Спустя пару минутту Ульяна вернулась с тарелкой, на которой лежала дымящаяся котлета."
    window hide

    scene cg d1_food_normal 
    with dissolve

    window show
    us "Держи, голодающий!"
    me "Спасибо..."
    "Хотя я не был уверен, что её было за что благодарить."

    stop music fadeout 3

    "Многочасовой голод дал о себе знать. Не тратя больше времени на Ульяну, я бодро подцепил вилкой котлету и..."
    window hide

    scene cg d1_food_skolop 
    with vpunch

    play music music_list["doomed_to_be_defeated"] fadein 0

    window show
    th "Ох, чёрт, это ещё что за хрень?!{w} Она живая!!!"
    window hide

    $ persistent.sprite_time = 'day'
    scene bg int_dining_hall_people_day 
    with dissolve

    play sound sfx_broken_dish
    play sound2 sfx_dropped_chair

    window show
    "Я со вскриком шлёпнулся на кафельный пол. В паре метров от меня приземлилась тарелка."
    "Нет, я не боялся ползучей инопланетной дряни, однако всё было череcчур внезапно для моего голодного сознания."
    th "И каждому воздастся по делам его..."
    me "Ах ты, мелкая!.."
    show us laugh2 pioneer far at center    with dissolve   
    "Ульяна уже стояла далеко у выхода и заливалась радостным детским смехом."

    stop ambience fadeout 2

    "Тихо бубня ругательства вперемежку с молитвами о безвременно ушедшем от меня ужине, я ринулся вслед за девчонкой."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_dining_hall_away_day 
    with dissolve

    window show
    "Мы выбежали из столовой."
    "Между нами была пара десятков метров, и я не ожидал трудной погони.{w} Всё-таки я старше и сильней."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_square_day 
    with dissolve

    window show
    "Мы пробежали мимо площади..."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_clubs_day 
    with dissolve

    window show
    "Помещения клубов..."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_path_day 
    with dissolve

    window show
    "Выбежали на лесную тропинку."
    "Я начал задыхаться."
    th "Проклятый Колумб! А впрочем, виноват тут только я."

    stop music fadeout 5

    "Я потерял Ульяну из виду."
    th "Я не смог догнать маленькую девочку!"
    th "Не смог..."
    "Я стоял и пытался отдышаться."
    "..."
    window hide

    with fade2

    window show
    "Стемнело."
    th "Сомневаюсь, что знаю, куда идти."
    th "Придётся наугад."
    "..."
    window hide

    with fade2

    window show
    "..."
    window hide

    $ persistent.sprite_time = 'sunset'
    scene bg ext_no_bus_sunset 
    with dissolve2

    $ sunset_time()

    play ambience ambience_camp_center_evening fadein 2

    window show
    "Немного погодя я вышел к воротам лагеря."
    me "Автобус исчез..."
    th "Не думаю, что мне позволили бы уехать на нём, в любом случае, даже неотложном."
    "Я задумался о том, что обошёл за сегодня весь лагерь, но водителя так и не видел."
    th "А был ли вообще водитель? Чертовщина какая-то."
    window hide

    with fade2

    window show
    "..."
    "Был поздний вечер, и искать что-либо впотьмах не имело смысла."
    th "Нужно узнать, где здесь я смогу поспать."

    stop ambience fadeout 2

    "..."
    window hide

    $ persistent.sprite_time = 'night'
    scene bg ext_no_bus_night 
    with dissolve2

    $ night_time()

    play ambience ambience_camp_center_night fadein 2

    window show
    "Я уже собирался идти назад, как вдруг..."
    show sl normal pioneer at center   with dissolve
    sl "Привет, что тут так поздно делаешь?"
    "Я вздрогнул."
    "Ох, это опять Славя. Сегодняшний день не пошёл на пользу моим нервам."
    show sl smile pioneer at center   with dspr
    sl "Не догнал Ульяну?"
    "Она улыбнулась."
    "Я кивнул."
    sl "Неудивительно.{w} Никто не может."
    th "Впечатляет."
    show sl normal pioneer at center   with dspr
    sl "Ты, наверное, есть хочешь, поужинать-то тебе не удалось."

    play sound sfx_stomach_growl channel 7

    "Ещё как. При одной лишь мысли о еде мой желудок мечтательно заурчал."
    show sl smile pioneer at center   with dspr
    "Славя улыбнулась."
    sl "Ну, тогда пойдём."
    me "Уже поздно. Разве столовая не закрыта?"
    sl "Да ничего, у меня ключи есть."
    me "Ключи?"
    sl "Да, у меня от всех помещений лагеря ключи есть."
    me "Почему?"
    sl "Ну, я здесь что-то вроде помощницы вожатой. Идем?"
    me "Да. Спасибо."
    window hide

    $ persistent.sprite_time = 'night'
    scene bg ext_square_night 
    with dissolve

    window show
    "Когда мы вышли на площадь, Славя внезапно остановилась."
    show sl normal pioneer at center   with dissolve
    sl "Слушай, мне надо соседку предупредить, что я вернусь позже, а то она такая пунктуальная – будет волноваться."
    sl "Ты иди пока к столовой, а я через минутку, хорошо?"
    me "Да..."
    window hide

    $ persistent.sprite_time = 'night'
    scene bg ext_dining_hall_away_night 
    with dissolve

    window show
    "Неожиданно, но у двери столовой уже кто-то был."

    play sound sfx_campus_door_rattle

    "Этот кто-то явно пытался открыть дверь. Безуспешно."
    window hide

    $ persistent.sprite_time = 'night'
    scene bg ext_dining_hall_near_night 
    with dissolve

    window show
    "Я, тактично стараясь топать погромче, поднялся на крыльцо."
    show dv normal pioneer at center   with dissolve
    "Алиса."
    th "Возможно, стоило подождать в сторонке..."
    "Некоторое время она, прищурившись, смотрела на меня. Затем сказала:"
    dv "Чё ты стоишь, помоги, что ли!"
    me "Что?"
    show dv angry pioneer at center   with dspr
    dv "Что-что... Дверь открыть, что!"
    me "Зачем?"
    dv "Сам угадаешь, или помочь?"
    me "Эээ...{w} А может, не стоит?"
    show dv smile pioneer at center   with dspr
    dv "А чё, сам ты жрать не хочешь, а?{w} Ульянка-то тебе поужинать нормально не дала!"
    "Она ехидно ухмыльнулась."
    me "Так сейчас Славя придет и..."
    show dv rage pioneer at center   with dspr
    dv "ЧЕГО?!"
    th "Похоже, не стоило мне этого говорить."
    show dv angry pioneer at center   with dspr
    dv "Я сваливаю!"
    dv "А с тебя я ещё спрошу!{w} Уже второй твой косяк!"
    "С этими словами она скрылась в ночи."
    hide dv  with dissolve
    th "Эм...{w} Когда был первый?"
    window hide

    with fade

    window show
    "Через некоторое время вернулась Славя."
    show sl normal pioneer at center   with dissolve

    stop ambience fadeout 2

    sl "Ну что, пойдём?"
    window hide

    $ persistent.sprite_time = 'night'
    scene bg int_dining_hall_night 
    with dissolve

    play music music_list["a_promise_from_distant_days"] fadein 5

    window show
    "Мы зашли в тёмную столовую."
    show sl normal pioneer at center   with dissolve
    sl "Подожди здесь, я сейчас что-нибудь принесу."
    hide sl  with dissolve
    "Я обессилено упал на стул и принялся покорно ждать свою спасительницу."
    window hide

    show cg d1_sl_dinner 
    with dissolve

    window show
    "Ужин был нехитрым – несколько булочек и стакан кефира."
    "Не то чтобы мой обычный ужин чем-то превосходил сегодняшний."
    "Пока я ел, Славя сидела напротив, смотря прямо на меня."
    me "У меня что-то на лице?"

    show cg d1_sl_dinner_0  with dspr

    sl "Нет, просто..."
    "Она по-доброму улыбнулась."
    sl "И как тебе первый день в лагере?"
    me "Ну, я даже не знаю..."
    "Сегодняшний день не был шибко плохим. На самом деле, всё происходящее вокруг меня...{w} радовало?"
    sl "Ничего, скоро привыкнешь!"

    show cg d1_sl_dinner  with dspr

    "Славя мечтательно уставилась в окно."
    th "Привыкну..."
    th "Как привыкли все они? Или же..."
    me "Ну а так, вообще здесь мило."
    "Сказал я, пытаясь прервать неловкое молчание."
    sl "Да?"
    "Без интереса спросила она."
    me "Да. Здесь так..."
    "Хотелось сказать «необычно», но я сдержался."
    "Обитатели лагеря не находили ничего здесь необычным.{w} Ну, или не показывали виду.{w} Говорить им об этом бессмысленно."
    sl "Как?"
    "Она внимательно посмотрела на меня так, будто от моего ответа зависело что-то серьезное."
    me "Ну, я не знаю... уютно. Очень уютно."

    show cg d1_sl_dinner_0  with dspr

    sl "Пожалуй."
    "Она вновь улыбнулась."
    sl "Очень хорошо, что ты так думаешь."
    me "Почему?"
    sl "Ну, не всем здесь нравится."
    me "А тебе?"
    sl "Мне?"
    me "Да."
    sl "Нравится, тут здорово."
    "Славя тихо засмеялась."
    "Я поражался, как естественно себя ведут все здешние дети."
    "Я вдруг почувствовал, что нахожу Славю милой."
    "Я украдкой посмотрел на неё, не зная, что еще добавить."

    show cg d1_sl_dinner  with dspr

    sl "Прости, надо было показать тебе лагерь, а я совсем забегалась сегодня."
    me "Да я сам вроде осмотрел всё более-менее. "

    show cg d1_sl_dinner_0  with dspr

    sl "Прям-таки всё-всё?"
    "Она улыбалась так, что мне приходилось от смущения прятать глаза."
    me "Ну почём я знаю – все или нет, я здесь первый день."
    sl "Ну хорошо, и что видел уже?"
    me "Площадь, столовую вот видел, футбольную площадку..."
    sl "А пляж?"
    me "Пляж?"
    sl "Обязательно сходи! Или давай вместе сходим!"
    me "А. Ну да, хорошо... сходим!"
    th "Пляж, футбольное поле... Так было во всех пионерлагерях?"
    "Было... В мою голову вдруг закралась одна идея, к которой я должен был прийти ещё много часов назад."
    "Может ли быть, что я попал в прошлое?"
    "Да, это бы всё объясняло."
    "Вошёл в автобус - и провалился в прошлое.{w} Потому я и сел зимой, а приехал уже летом! Точно."
    me "А можно задать глупый вопрос?"
    sl "Да, конечно, но..."
    window hide

    stop music fadeout 5

    $ persistent.sprite_time = 'night'
    scene bg int_dining_hall_night 
    show sl normal pioneer at center 
    with dissolve

    window show
    "Славя быстро встала из-за стола."
    sl "Ой, уже так поздно... Сам дорогу до Ольги Дмитриевны найдёшь?"
    me "Найду, конечно, но зачем?"
    sl "В домик тебя поселит к кому-нибудь."
    me "Зачем?"
    show sl laugh pioneer at center   with dspr
    sl "Тебе не помешает изрядка где-то спать!"
    me "Разумно."
    show sl smile pioneer at center   with dspr
    sl "Ладно, я побежала тогда.{w} Спокойной ночи!"
    me "Спокойной ночи!"
    
    hide sl  with dissolve

    th "Это моё воображение, или она исчезла слишком внезапно? И именно когда..."
    "Мое внимание привлекла связка ключей, торчавшая из двери."
    "Я хотел было броситься за Славей, но рассудил, что понятия не имею, где она живет."
    window hide

    menu:
        "Взять ключи":
            window show
            $ d1_keys = True
            th "Мало ли, что может произойти. Возьму с собой, а завтра отдам при встрече."
            window hide
        "Не трогать":
            window show
            th "Потеряю еще. Пусть тут будут."
            window hide

    $ persistent.sprite_time = 'night'
    scene bg ext_dining_hall_near_night 
    with dissolve

    play ambience ambience_camp_center_night fadein 5

    window show
    "Это был насыщенный день. Глаза слипались, и я поспешно двинулся к домику вожатой."
    window hide

    $ persistent.sprite_time = 'night'
    scene bg ext_square_night 
    with dissolve

    window show
    "Проходя мимо площади, я с удивлением встретил Лену. Она, как и днём, сидела на одной из лавочек и читала книгу."

    play music music_list["lets_be_friends"] fadein 5

    show un normal pioneer far at center    with dissolve   
    "Я решил подойти и поговорить."
    "Она единственная, с кем мне не удалось сегодня перекинуться и парой слов."
    show un normal pioneer at center   with dissolve
    me "Э-э... Привет! Читаешь, да?"
    show un surprise pioneer at center   with dspr
    "Лена от удивления аж подпрыгнула на лавочке."
    me "И-извини, не хотел тебя напугать!"
    show un shy pioneer at center   with dspr
    un "Ничего..."
    "Она покраснела и снова уставилась в книжку."
    me "Что читаешь?"
    "Она показала обложку – «Унесённые ветром»."
    window hide

    menu:
        "Похвалить книгу":
            $ lp_un = (lp_un) + (1)
            window show
            "Я не знаю почему, но мне вдруг захотелось хоть как-то приободрить эту застенчивую девушку. "
            me "Хорошая книжка."
            "Делано бодрым голосом сказал я, мучительно пытаясь вспомнить, о чем в ней идет речь."
            un "С-спасибо..."
            "Пролепетала она, не поднимая на меня глаз. Я чувствовал себя невероятно глупо, ведь сам эту книгу не читал и не знал, про что она, но раз уж я заговорил, придётся как-то выкручиваться."
            me "Она вроде бы вызвала много шума в свое время."
            "Ляпнул я, так ничего и не придумав."
            "Она на секунду замешкалась, словно размышляя, ответить мне или промолчать, но все же произнесла:"
            un "Но ведь если книга не вызывает споров, то зачем она вообще нужна?"
            "Честно говоря, такое мне в голову не приходило. Я вообще не мог вспомнить когда читал что-то кроме сообщений в интернете."
            "А ведь с этой неловкостью в общении мы друг друга стоим."
        "Ничего не говорить":
            window show
            pass

    "Я не собирался затягивать диалог."
    me "Ох, я, наверно, мешаю тебе..."
    show un normal pioneer at center   with dspr
    un "Нет."
    "Не отрывая взгляд от книжки, сказала она."
    me "Я присяду?"
    show un surprise pioneer at center   with dspr
    un "Зачем?"
    th "Действительно - зачем? Не похоже, чтобы я мог у нее хоть что-то выпытать."
    show un shy pioneer at center   with dspr
    "Я внимательно осмотрел Лену, от чего та покраснела."
    th "Возможно, стоит уйти."
    me "Я всё-таки мешаю, поэтому..."
    un "Нет, не мешаешь."
    me "Да нет, я могу и уйти..."
    un "Все в порядке."
    "Ситуация становилась нелепой."
    me "Ну, раз так..."
    "Я аккуратно сел на край лавочки."
    "После такого разговора мне меньше всего хотелось вообще находиться здесь, но и встать и уйти было как-то неудобно."
    me "Как-то нехорошо получилось..."
    "Лена ничего не ответила."
    "Я выставил себя дураком. Одно из моих хобби, похоже."
    "Будь на её месте, допустим, Ульянка, наверняка бы меня засмеяла."
    "Я вспомнил вопрос Слави и решил, что он как нельзя лучше подойдет для начала разговора."
    me "А тебе здесь нравится?"
    show un smile pioneer at center   with dspr
    un "Да."
    "Она едва заметно улыбнулась."
    me "И мне, наверное, нравится..."
    show un normal pioneer at center   with dspr
    "Было очевидно, что Лена не очень общительна и вряд ли в состоянии поддержать непринужденную беседу ни о чем, как Славя."
    "Но в то же время было в ней что-то, что привлекало внимание."
    "Что-то, скрытое от глаз, но такое родное и естественное."
    show un shy pioneer at center   with dspr
    un "Поздно уже, мне пора..."
    me "Да, поздновато."
    un "Спокойной ночи."
    me "Спокойной..."
    hide un  with dissolve
    "Что-то с ней не так."
    th "Вроде бы обычная скромная девочка, но..."
    "Ещё один вопрос без ответа. Как неожиданно."

    stop music fadeout 5
    "..."
    window hide

    with fade2

    window show

    stop ambience fadeout 2

    "Я продолжил свой путь к домику вожатой."
    window hide

    $ persistent.sprite_time = 'night'
    scene bg ext_house_of_mt_night 
    with dissolve

    window show
    "В окне горел свет."
    window hide

    $ persistent.sprite_time = 'sunset'
    scene bg int_house_of_mt_noitem_night 
    with dissolve

    play music music_list["smooth_machine"] fadein 5

    show mt normal pioneer at center   with dissolve

    window show

    mt "Привет, Семен!{w} Что-то ты долго!"
    me "Да...{w} Гулял, с лагерем знакомился."
    mt "Хорошо.{w} Спать будешь здесь."
    "Она указала на одну из кроватей."
    me "Здесь?.."
    "Я недоверчиво моргнул."
    show mt surprise pioneer at center   with dspr
    mt "Ну да, а что такого?{w} Все равно других свободных мест нет."
    show mt smile pioneer at center   with dspr
    mt "Ты же хочешь стать порядочным пионером?"
    "Она акцентировала внимание на слове «порядочным»."
    if d1_mt_good_relationship:
        "Я решил не задавать лишних вопросов и сказать ей то, что она хочет услышать."
        "Другими словами, втереться в доверие."
        me "Да... Конечно..."
        me "Спасибо за пионерскую форму, я, кажется, забыл тогда Вас поблагодарить."
        show mt surprise pioneer at center   with dspr
        mt "Не стоит, ведь не могла же я тебе позволить ходить по лагерю в одних трусах?"
        show mt laugh pioneer at center   with dspr
        mt "Ладно, уже поздно, пора на боковую!"
        show mt normal pioneer at center   with dissolve
        me "Действительно... Я сильно устал."
        mt "Ну так тем более."
        "Я снял одежду и сложил её на тумбочке, а сам лёг в кровать, как только расправил её."
        "Ольга Дмитриевна погасила свет и, судя по шороху, принялась раздеваться перед сном."
        "Я тактично повернулся лицом к стенке."
        "Пару минут спустя я сказал в темноту:"
        me "Доброй ночи, Ольга Дмитриевна."
        mt "И тебе доброй ночи."
        jump day1_dreaming
    "Я задумался на мгновение."
    me "Но ты-то не против?"
    show mt surprise pioneer at center   with dspr
    "Она как-то странно на меня посмотрела.{w} В её взгляде смешались удивление и что-то вроде обиды."
    show mt angry pioneer at center   with dspr
    mt "Пионер должен уважать старших!"
    "Грозно сказала она."
    me "Должен, конечно, кто же спорит..."
    "Пролепетал я в ответ, не понимая, что же не так."
    mt "Или ты?.."
    show mt rage pioneer at center   with dspr
    "Она пристально посмотрела на меня."
    "Я почувствовал, как по щеке пробежала капелька пота."
    me "Я? А что я?{w} Да что не так-то?!"
    "Из-за слегка панического состояния я невольно перешел на тон выше."
    mt "Взрослых надо называть на Вы!"
    "Я внимательно на неё посмотрел. Неужели она настолько манерная? Если она и старше меня, то максимум на пару лет."
    th "Возможно, сейчас не лучшее время для споров."
    "Да и, положа руку на сердце, меня не слишком волновало, обращаться к ней на «ты» или на «Вы». Так что я смущенно промямлил:"
    me "Э-э... извините, виноват..."
    show mt smile pioneer at center   with dspr
    mt "Так-то лучше!{w} Так и должен вести себя порядочный пионер!"
    mt "А теперь спать!"
    "У меня не было планов по становлению пионером, но при упоминании сна я растерял последнюю охоту о чём-либо спорить."
    "Я облегчённо рухнул на кровать."
    window hide

    stop music fadeout 5
    jump day1_dreaming

label day1_sitting_home:
    $ persistent.sprite_time = 'day'
    scene bg int_house_of_mt_noitem_day 
    with dissolve
    
    window show
    "И вот, я снова был полностью предоставлен самому себе, как в небольшой квартирке, где прожил несколько лет."
    "По привычке я сейчас должен включить системный блок..."
    "Я откинулся на спинку стула."
    "Гудят блок питания с кулером, загорается синим лампочка под экраном монитора..."
    "Я положил руки перед собой, будто на клавиатуру, и вздохнул."
    "Здесь я лишился самого нужного предмета в моей жизни, который позволял коротать время, превращая годы в один сплошной день."
    "День без разнообразия."
    "Но приятный, как сон, а всё благодаря виртуальному миру."
    "Факт есть факт - лагерь отнял у меня возможность забыться, пообщаться по-человечески с единомышленниками."
    th "Что мне теперь делать?"
    "Я застыл, как роденовский мыслитель."
    "Удивительно, но ломки по компьютеру я не испытал, вместо неё пришла скука."
    th "Нет, на улицу я не пойду. Но как же я покину лагерь?"
    th "Вот забавная ситуация, заехал, а выехать не смог из-за непоколебимого духа затворника."
    "Непоколебимый... Пока что этот эпитет - не про меня."
    "Я резко встал и обогнул столик, чтобы повернуть к себе будильник, стоявший боком на подоконнике."
    "Было три часа дня."
    th "По идее, в лагерях и детских садах в это время идёт тихий час..."
    "Я посмотрел на идеально заправленную койку."
    th "Не буду же я сейчас ложиться спать? Вокруг неизвестные мне люди, я даже не знаю, в каком я городе..."
    "Невольно я дёрнулся к выходу, чтобы пойти на улицу, но вдруг в голове снова взыграла боль. Если выспаться, может, будет легче."
    "Койка была свободной, ничьих вещей под ней я не обнаружил."
    "Чувствуя в своих действиях какое-то принуждение, я разметал бельё на кровати, скинул обувь и лёг под простынь, желая поскорее уснуть."
    "..."
    $ renpy.pause(3) #TODO Написать, что Семён просыпается в шесть часов вечера, диалог с Мод-тян, вечерние ивенты и ужин.
    
    stop ambience
    window hide

    jump day2_main1

label day1_dreaming:

    scene bg black 
    with fade2

    window show
    "В голове проносились расплывчатые воспоминания о сегодняшнем дне."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_bus 
    show prologue_dream 
    with fade

    window show
    "Автобус..."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_square_day 
    show prologue_dream 
    with fade

    window show
    "Площадь. Памятник..."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg int_dining_hall_people_day 
    show us laugh pioneer at center 
    show prologue_dream 
    with fade

    window show
    "Столовая, полная народу...{w} Довольное лицо Ульяны."
    window hide

    $ persistent.sprite_time = 'day'
    scene bg ext_boathouse_day 
    show sl smile swim at center 
    show prologue_dream 
    with fade

    window show
    "Славя..."
    window hide

    scene bg ext_square_night 
    show un night at center 
    show prologue_dream 
    with fade

    window show
    "Лена..."
    window hide

    scene bg ext_houses_day 
    show dv angry pioneer2 at center 
    show prologue_dream 
    with fade
    $ persistent.sprite_time = 'day'

    window show
    "Алиса..."
    window hide

    scene black 
    with fade3

    window show
    th "А почему я вообще хочу выбраться отсюда?"
    window hide

    $ renpy.pause(3)

    jump day2_main1
